@precedence {
  call @right
}

@top Program { statement* Terminator }

@top SingleExpression { expression }

Terminator { dotdot }

statement[@isGroup=Statement] {
  declaration |
  expression
}

declaration {
  LetDeclaration
}

LetDeclaration {
  kw<"let"> LetDeclarator Terminator
}

LetDeclarator {
  pattern ("=" statement*)?
}

pattern { BindingName | ArrayPattern | ObjectPattern }

ArrayPattern { "[" spaceSep<pattern>? "]" }

ObjectPattern { "{" spaceSep<PatternProperty>? "}" }

PatternProperty {
  BindingName ":" pattern
}

expression[@isGroup=Expression] {
  primaryExpression //|
}

primaryExpression {
  Number |
  String |
  TemplateString |
  Name |
  boolean |
  kw<"null"> |
  ArrayExpression |
  ObjectExpression |
  ParenthesizedExpression |
  Lambda
}

values {
  primaryExpression+
}

//BinaryExpression[@dynamicPrecedence=-1] {
//  expression !binary expression
//}

ParenthesizedExpression { "(" expression ")" }

Lambda {
  "<" identifier* ":" expression ">"
}

ArrayExpression {
  "[" spaceSep<expression>? "]"
}

ObjectExpression {
  "{" spaceSep<Property>? "}"
}

spaceSep<content> {
  content+
}

propName { BindingName | "[" expression "]" | Number | String }

Property {
  propName (":" expression)?
}

@skip {} {
  TemplateString[isolate] {
    templateStart (templateEscape | templateContent | templateExpr)* templateEnd
  }

  String[isolate] {
    '"' (stringContentDouble | Escape)* ('"' | "\n") |
    "'" (stringContentSingle | Escape)* ("'" | "\n")
  }
}

templateExpr[@name=Interpolation,isolate] { InterpolationStart expression? InterpolationEnd }

kw<term> { @specialize[@name={term}]<identifier, term> }

boolean { @specialize[@name=BooleanLiteral]<identifier, "true" | "false"> }

Name { identifier }

BindingName { identifier }

@skip {
  spaces |
  newline |
  LineComment
}

@context trackNewline from "./tokens.js"

@local tokens {
  InterpolationStart[closedBy=InterpolationEnd] { "${" }
  templateEnd { "`" }
  templateEscape[@name=Escape] { Escape }
  @else templateContent
}

@tokens {
  spaces[@export] { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
  newline[@export] { $[\r\n\u2028\u2029] }

  LineComment[isolate] { "//" ![\n]* }

  dotdot { ".." }

  identifierChar { @asciiLetter | $[_$\u{a1}-\u{10ffff}] }

  identifier { identifierChar (identifierChar | @digit | "-")* }

  @precedence { spaces, newline, identifier }

  hex { @digit | $[a-fA-F] }

  Number {
    '-'? (@digit ("_" | @digit)* ("." ("_" | @digit)*)? | "." @digit ("_" | @digit)*)
      (("e" | "E") ("+" | "-")? ("_" | @digit)+)? |
    @digit ("_" | @digit)* "n" |
    "0x" (hex | "_")+ "n"? |
    "0b" $[01_]+ "n"? |
    "0o" $[0-7_]+ "n"?
  }

  Escape {
    "\\" ("x" hex hex | "u" ("{" hex+ "}" | hex hex hex hex) | ![xu])
  }

  stringContentSingle { ![\\\n']+ }

  stringContentDouble { ![\\\n"]+ }

  templateStart { "`" }

  InterpolationEnd[openedBy=InterpolationStart] { "}" }

  "="[@name=Equals]
  "..."[@name=Spread]
  "=>"[@name=Arrow]

  "(" ")" "[" "]" "{" "}" "<" ">"

  "." "," ";" ":" "@"
}

@external propSource jsHighlight from "./highlight"

@detectDelim